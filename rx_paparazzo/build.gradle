apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.miguelbcr'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "io.reactivex.rxjava2:rxjava:2.1.16"
    compile "com.github.VictorAlbertos:RxActivityResult:0.4.5-2.x"
    compile 'com.github.tbruyelle:rxpermissions:0.10.2'
    compile 'com.github.yalantis:ucrop:2.2.2'
    compile 'com.android.support:exifinterface:27.1.1'

    testCompile 'junit:junit:4.12'
}

configurations.all {
    resolutionStrategy.force "com.android.support:exifinterface:27.1.1"
    resolutionStrategy.force "com.android.support:animated-vector-drawable:27.1.1"
    resolutionStrategy.force "com.android.support:appcompat-v7:27.1.1"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'com.github.miguelbcr'
                pom.groupId = 'com.github.miguelbcr'
                pom.artifactId = 'RxPaparazzo'
                pom.version = '2.x-SNAPSHOT'
            }
        }
    }
}
